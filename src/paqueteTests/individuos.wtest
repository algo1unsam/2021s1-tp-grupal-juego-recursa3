import paqueteSecundario.individuos.*
import paqueteSecundario.objetos.*
import wollok.game.*
import paquetePrimario.configuracion.*
import paquetePrimario.menu.*
import paqueteSecundario.direcciones.*
import paqueteSecundario.estados.*
describe "TEST DE INDIVIDUO" {

	const enemigo0 = new Enemigo(vida = 2, ataque = 1, position = game.at(5, 6), orientacion = arriba, imagen = "enemigo/enemigoZombieChicoDerecha.png", categoria = 'enemigo',posicionCorazon1 = menu.posicionCorazonEnemigo1(), posicionCorazon2 = menu.posicionCorazonEnemigo2(), posicionCorazon3 = menu.posicionCorazonEnemigo3())
	const llaveCofre = new Llave(categoria = "llave", peso = 0, imagen = "llave/llaveCofre.png", position = menu.posicionLlaveCofre())
	const llaveCofre2 = new Llave(categoria = "llave", peso = 0, imagen = "llave/llaveCofre.png", position = menu.posicionLlaveCofre())
	
	fixture {
		personaje.position(game.at(5, 5))
		personaje.orientacion(arriba)
		game.addVisual(enemigo0)
		game.addVisual(personaje)
		personaje.agregarObjeto(mochilaGrande)
		personaje.agregarObjeto(espadaChica)
		personaje.agregarObjeto(escudoGrande)
		personaje.agregarObjeto(llaveCofre2)
		personaje.agregarObjeto(llaveCofre)
		game.onTick(1000, "perseguir" + enemigo0, { true}) // Se hardcodea porque el enemigo en el juego se mueve y es necesario para testear.
		configuraciones.configurarColisiones()
		
		
	}

	test "EL INDIVIDUO TIENE MOCHILA GRANDE Y DENTRO DE LA MOCHILA TIENE UNA ESPADA CHICA Y UN ESCUDO GRANDE Y DOS LLAVES" {
		assert.equals(mochilaGrande, personaje.mochila())
		assert.equals([ espadaChica, escudoGrande, llaveCofre2,llaveCofre], mochilaGrande.objetosGuardados())
	}

	test "EL INDIVIDUO DECIDE DEJAR UN OBJETO PORQUE LE OCUPA ESPACIO" {
		personaje.desecharObjeto(espadaChica)
		assert.equals([ escudoGrande,llaveCofre2,llaveCofre], mochilaGrande.objetosGuardados())
	}

	test "EL INDIVIDUO AL AGARRAR LA ESPADA GRANDE Y MANTIENE AMBAS ESPADAS PERO OCUPA LA ESPADAGRANDE." {
		personaje.agregarObjeto(espadaGrande)
		assert.that(mochilaGrande.objetosGuardados().any({ objeto => objeto == espadaChica}))
		assert.notThat(personaje.mano1() == espadaChica)
		assert.that(mochilaGrande.objetosGuardados().any({ objeto => objeto == espadaGrande}))
		assert.that(personaje.mano1() == espadaGrande)
	}

	test "EL personaje AGARRA 3 LLAVES MAS Y UTILIZA UNA, LE QUEDAN 4" {
		3.times({ i => personaje.agregarObjeto(llaveCofre)})
		personaje.utilizarObjeto(llaveCofre)
		assert.equals(4, personaje.cantidadLlaves())
	}

	test "EL personaje ATACA A UN RIVAL HASTA DEJARLO CON 1 VIDA" {
		personaje.atacar()
		assert.equals(1, enemigo0.vida())
	}

	test "EL personaje ATACA AL enemigo0 HASTA MATARLO" {
		2.times({ i => personaje.atacar()})
		assert.equals(0, enemigo0.vida())
		assert.notThat(game.hasVisual(enemigo0))
	}

	test "EL ENEMIGO ATACA personaje, Y ESTE QUEDA CON 5 PUNTOS DE VIDA." {
		personaje.moverse(arriba)

		assert.equals(personaje.position(), enemigo0.position())
		assert.equals(5, personaje.vida())
	}
	test "Me devuelve una llave"{
		assert.equals(llaveCofre2,personaje.mochila().objetosGuardados().find({objeto => objeto.categoria()=='llave'}))
		
	}
}

